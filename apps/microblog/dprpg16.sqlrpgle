     H option(*nodebugio)
     Fdp16      cf   e             workstn
     Ftwuser    uf a e           k disk

     D fieldError      s               n
     D keyError        s               n

       *in03 = *off;
       *in12 = *off;
       dow *in03 = *off and *in12 = *off;
        dptit = 'User ABM';
        dpfoot = 'F3=Exit  F6=Add  F12=Back';
        write header;
        write footer;
        exfmt main;
        select;
         when *in03;
         when *in06;
          exsr srAdd;
          *in06 = *off;
         when *in12;
         other;
          exsr srVKey;
          if keyError;
           iter;
          endif;
          chain dpusid twuser;
          if not %found;
           *in51 = *on;
           dpmsg = 'User not found';
          else;
           exsr srSecond;
          endif;
        endsl;
       enddo;
       *inlr = *on;

       // Subroutines
       begsr srSecond;
        dpusname = usname;
        dpusmail = usmail;
        dpusbio = usbio;
        dpuscrts = uscrts;
        *in50 = *on;
        dow *in03 = *off and *in12 = *off;
         dptit = 'User read/update';
         dpfoot = 'F3=Exit  F12=Back  F23=Delete';
         write header;
         write main;
         write footer;
         exfmt second;
         select;
          when *in03;
          when *in12;
          when *in23;
           exsr srDelete;
           *in23 = *off;
          other;
           exsr srVFields;
           if fieldError;
            iter;
           endif;
           usname = dpusname;
           usmail = dpusmail;
           usbio = dpusbio;
           update twuserr;
           *in12 = *on;
         endsl;
        enddo;
        *in12 = *off;
        *in50 = *off;
       endsr;

       begsr srAdd;
        exec sql select max(usid) into :dpusid from twuser;
        dpusid = dpusid + 1;
        dpusname = '';
        dpusmail = '';
        dpusbio = '';
        dpuscrts = %timestamp();
        *in50 = *on;
        dow *in03 = *off and *in12 = *off;
         dptit = 'User add';
         dpfoot = 'F3=Exit  F12=Back';
         write header;
         write main;
         write footer;
         exfmt second;
         select;
          when *in03;
          when *in12;
          when *in23;
          other;
           exsr srVFields;
           if fieldError;
            iter;
           endif;
           usid = dpusid;
           usname = dpusname;
           usmail = dpusmail;
           usbio = dpusbio;
           uscrts = %timestamp();
           write twuserr;
           *in12 = *on;
         endsl;
        enddo;
        *in12 = *off;
        *in50 = *off;
       endsr;

       begsr srVKey;
        *in51 = *off;
        keyError = *off;
        dpmsg = '';
        if dpusid < 1;
         *in51 = *on;
         dpmsg = 'User ID must be greater than 0';
        endif;
        if *in51;
         keyError = *on;
        endif;
       endsr;
       begsr srVFields;
        *in52 = *off;
        *in53 = *off;
        *in54 = *off;
        fieldError = *off;
        dpmsg = '';
        if dpusname = *blanks;
         dpmsg = 'User name cant be blank';
         *in52 = *on;
        elseif dpusmail = *blanks;
         dpmsg = 'User email cant be blank';
         *in53 = *on;
        endif;
        if *in52 or *in53 or *in54;
         fieldError = *on;
        endif;
       endsr;

       begsr srDelete;
        dwusname = dpusname;
        dwusmail = dpusmail;
        exfmt confirm;
        select;
         when *in12;
          *in12 = *off;
         other;
          delete twuserr;
          *in12 = *on;
        endsl;
       endsr;
